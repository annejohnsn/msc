FastPartialRhoSeq := proc (A, p:=[2,3]) 
# Given a set of distances A for a product spaces specified in p, compute the exponent of each prime in the partial characteristic sequence 
# arg A; a vector indicating the sequence of decreasing distances in Z_p_i
# arg p; a list of prime numbers indicating the components of the product space
# return; the beta(k)^th term in the characteristic sequence, where beta is the structure sequence and k is the length of A

	local g, h,computePowers,n, shortA, primeExponents,i, thisPrime, thisPrimeIndex,B, G, powers, powersOfG, thisPrimeSum;
	
	#=== Some helper functions ===#
	#Return the index of every instance of p-multiples in a list 
	#Use to find the index of a given prime in A
	h := proc(i,L,p) if L[i] mod p = 0 then return i else return NULL fi; end proc;	

	#Count the number of times the mth element has appeared as a factor for the 1..m first elements in a list
	#Use to compute the (decreasing) sequence of distances in A or a subset of A
	g := proc(m, L)
		local basePrime;
		
		basePrime := L[m];
		
		#since we just want the exponent not actual distance just compute what power this would be
		return ordp(mul(L[1..m]), basePrime); 

	end proc;
	
	#Compute the appropriate power of an element of G
	computePowers := proc(m, L)
		local power;
	
		if m=nops(L) then
			power:= L[-1]-1;
		else 
			power:= mul(L[(m+1)..nops(L)])* (L[m]-1);
		end if;
		return power
	end proc;
	
	#compute n, then create a copy of A with the first element deleted to ease the indexing
	n := mul(A);
	shortA := A[2..nops(A)];

	#compute the terms corrsponding to each prime given 
	primeExponents := Vector();
	for i from 1 to nops(p) do
		#pull out the prime 
		thisPrime := p[i];

		#first get the index in A of this prime
		thisPrimeIndex := map(h, [seq(i, i=1..nops(shortA))], shortA, thisPrime);
		B:= shortA[thisPrimeIndex];

		#then find the (exponents for the) distances occuring with this prime
		G:= map(g,[seq(i, i=1..nops(B))],B);
	
		#Figure out what power each distance should be raised to
		powers := map(computePowers,thisPrimeIndex, shortA);
	
		#raise each element in G to the given powers
 		powersOfG:= zip(proc (x, y) options operator, arrow; x*(y) end proc, G, powers); 
		
		#the exponent of this prime in the nth partial will be -([the sum of the elements in powers]/n), where n is the product of elements in A (including the first element)
		thisPrimeSum := add(powersOfG);
		primeExponents(i) := thisPrimeSum/n; 
		
 	end do;

	return primeExponents;

end proc;
